&НаКлиенте
Перем Подключение;
Перем СтрокаТЧПрокси;
Перем ЗаголовкиЗапросов;

&НаКлиенте
Функция ВыбратьПрокси(НастройкиПодключения)
	Для Каждого Стр из Объект.ПроксиСервера Цикл
		Если Стр.Работает И Стр.КоличествоЗапросов <= Объект.КоличествоЗапросовЧерезОдинСервер Тогда
			Прокси = Новый ИнтернетПрокси;
			Прокси.Установить(?(НастройкиПодключения.ЗащищенноеСоединение,"https", "http"), Стр.Адрес, Стр.Порт);
			СтрокаТЧПрокси = Стр;
			ЕстьРаботающиеПрокси = Истина;
			Возврат Прокси;
		КонецЕсли;
	КонецЦикла;
КонецФункции

&НаКлиенте
Процедура ПроверитьСписокПрокси()
	а = 1;
	Для Каждого Стр из Объект.ПроксиСервера Цикл
		Состояние("Проверка прокси сервера: " + а + " из " + Объект.ПроксиСервера.Количество(),а/Объект.ПроксиСервера.Количество()*100);
		а = а + 1;
		Стр.КоличествоЗапросов = 0;
		Стр.Работает = ПроксиРаботает(Новый Структура("Адрес, Порт", Стр.Адрес, Стр.Порт));
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПодключение(АдресСервера = "")
	Если Подключение = Неопределено Тогда
		ЗащищенноеСоединение = Неопределено;
		Если АдресСервера <> "" Тогда
			НастройкиПодключения = Новый Структура("АдресСайта", АдресСервера);
		Иначе
			НастройкиПодключения = Новый Структура("АдресСайта", Объект.НачальныйАдрес);
		КонецЕсли;
		РазобратьАдресСайта(НастройкиПодключения);
		Если НастройкиПодключения.ЗащищенноеСоединение Тогда
			
			ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(
				Неопределено,
				Неопределено);
			
			КонецЕсли;
		Прокси = Неопределено;
		
		Если Объект.ПроксиСервера.Количество() > 0 Тогда
			Прокси = ВыбратьПрокси(НастройкиПодключения);
			Если Прокси = Неопределено Тогда
				ПроверитьСписокПрокси();
				Прокси = ВыбратьПрокси(НастройкиПодключения);
				Если Прокси = Неопределено Тогда
					Сообщить("Ни один прокси не работает!");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Выполнить(Объект.КомандаПодключения);
		ИнициализироватьЗаголовкиЗапроса();
	КонецЕсли;
КонецПроцедуры  

&НаКлиенте
Функция ИнициализироватьЗаголовкиЗапроса()
	ЗаголовкиЗапросов = Новый Соответствие;
	Для Каждого КлючЗначение из Объект.ЗаголовкиЗапросов Цикл
		ЗаголовкиЗапросов.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);	
	КонецЦикла;
КонецФункции
 &НаКлиенте
Функция РазобратьАдресСайта(НастройкиПодключения)
	
	АдресСайта = СокрЛП(НастройкиПодключения.АдресСайта); 
	
	Сервер = ""; 
	
	Порт = 0;
	
	АдресСкрипта = "";
	
	ЗащищенноеСоединение = Ложь;
	
	Если НЕ ПустаяСтрока(АдресСайта) Тогда
		
		АдресСайта = СтрЗаменить(АдресСайта, "\", "/");
		АдресСайта = СтрЗаменить(АдресСайта, " ", "");
		
		Если НРег(Лев(АдресСайта, 7)) = "http://" Тогда
			АдресСайта = Сред(АдресСайта, 8);
		ИначеЕсли НРег(Лев(АдресСайта, 8)) = "https://" Тогда
			АдресСайта = Сред(АдресСайта, 9);
			ЗащищенноеСоединение = Истина;
		КонецЕсли;
		
		ПозицияСлэша = СтрНайти(АдресСайта, "/");
		
		Если ПозицияСлэша > 0 Тогда
			Сервер = Лев(АдресСайта, ПозицияСлэша - 1);
			АдресСкрипта = Прав(АдресСайта, СтрДлина(АдресСайта) - ПозицияСлэша);
		Иначе	
			Сервер = АдресСайта;
			АдресСкрипта = "";
		КонецЕсли;
		
		ПозицияДвоеточия = СтрНайти(Сервер, ":");
		ПортСтрока = "0";
		Если ПозицияДвоеточия > 0 Тогда
			СерверСПортом = Сервер;
			Сервер = Лев(СерверСПортом, ПозицияДвоеточия - 1);
			ПортСтрока = Прав(СерверСПортом, СтрДлина(СерверСПортом) - ПозицияДвоеточия);
		КонецЕсли;
			
		Порт = Число(ПортСтрока);
		
		Если Порт = 0 Тогда
			Порт = ?(ЗащищенноеСоединение, 443, 80);
		КонецЕсли;
		
	КонецЕсли;
	
	НастройкиПодключения.Вставить("Сервер", Сервер); 
	НастройкиПодключения.Вставить("Порт", Порт);
	НастройкиПодключения.Вставить("АдресСкрипта", АдресСкрипта);
	НастройкиПодключения.Вставить("ЗащищенноеСоединение", ЗащищенноеСоединение);
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьЗапросПроксе(ИнтернетПрокси, ЗащищенноеСоединение)
	ИмяПротокола = "http";
	Если ЗащищенноеСоединение Тогда
		ИмяПротокола = "https";
	КонецЕсли;
	СтрокаТЧПрокси = Объект.ПроксиСервера.НайтиСтроки(Новый Структура("Адрес, Порт", ИнтернетПрокси.Сервер(ИмяПротокола), ИнтернетПрокси.Порт(ИмяПротокола)));
	Если СтрокаТЧПрокси.Количество() > 0 Тогда
		Если СтрокаТЧПрокси[0].КоличествоЗапросов > Объект.КоличествоЗапросовЧерезОдинСервер Тогда
			Подключение = Неопределено;
		Иначе
			СтрокаТЧПрокси[0].КоличествоЗапросов = СтрокаТЧПрокси[0].КоличествоЗапросов + 1;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПроксиНерабочим(ИнтернетПрокси, ЗащищенноеСоединение)  
	
	ИмяПротокола = "http";
	Если ЗащищенноеСоединение Тогда
		ИмяПротокола = "https";
	КонецЕсли;
	СтрокаТЧПрокси = Объект.ПроксиСервера.НайтиСтроки(Новый Структура("Адрес, Порт", ИнтернетПрокси.Сервер(ИмяПротокола), ИнтернетПрокси.Порт(ИмяПротокола)));
	Если СтрокаТЧПрокси.Количество() > 0 Тогда
		СтрокаТЧПрокси[0].Работает = Ложь;
		Подключение = Неопределено;
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Функция ПроксиРаботает(АдресПорт) 
	
	ЗащищенноеСоединение = Неопределено;
	НастройкиПодключения = Новый Структура("АдресСайта", Объект.НачальныйАдрес);
	РазобратьАдресСайта(НастройкиПодключения);
	Если НастройкиПодключения.ЗащищенноеСоединение Тогда
		
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(
			Неопределено,
			Неопределено);
		
	КонецЕсли;
	Прокси = Новый ИнтернетПрокси;
	Прокси.Установить(?(НастройкиПодключения.ЗащищенноеСоединение,"https", "http"), АдресПорт.Адрес, Число(АдресПорт.Порт));
	Подключение = Новый HTTPСоединение(
		НастройкиПодключения.Сервер,
		НастройкиПодключения.Порт,
		,
		,Прокси
		,
		10,
		ЗащищенноеСоединение);
		
	Запрос = Новый HTTPЗапрос(Объект.НачальныйАдрес);
	Попытка
		Ответ = Подключение.Получить(Запрос);
		Если Ответ.КодСостояния <> 200 И Ответ.КодСостояния <> 301 Тогда
			Возврат Ложь;
		КонецЕсли;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина; 
	
КонецФункции 

&НаКлиенте
Процедура ПослеЗагрузкиПрокси(ВыбранныеФайлы, Параметры) Экспорт
 
	Если Не ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		Возврат;
	КонецЕсли;
 
	Текст = Новый ТекстовыйДокумент;
	Текст.Прочитать(ВыбранныеФайлы[0]);
	Для а = 1 По Текст.КоличествоСтрок() Цикл
		Состояние("Проверка прокси сервера: " + а + " из " + Текст.КоличествоСтрок(),а/Текст.КоличествоСтрок()*100);
		Стр = Текст.ПолучитьСтроку(а);
		АдресПорт = РазложитьСтрокуВМассив(Стр, ":");
		Если АдресПорт.Количество() > 1 Тогда
			Если ПроксиРаботает(Новый Структура("Адрес, Порт", АдресПорт[0], АдресПорт[1])) Тогда
				СтрокаПрокси = Объект.ПроксиСервера.Добавить();
				СтрокаПрокси.Адрес = АдресПорт[0];
				СтрокаПрокси.Порт = АдресПорт[1];
				СтрокаПрокси.Работает = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Подключение = Неопределено;
	
КонецПроцедуры 

&НаКлиенте
Функция РазложитьСтрокуВМассив(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено) Экспорт
	
	Результат = Новый Массив;
	
	// для обеспечения обратной совместимости
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Результат.Добавить(Подстрока);
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Результат.Добавить(Строка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ИмпортПрокси(Команда)
	НастройкиПодключения = Новый Структура("АдресСайта", Объект.НачальныйАдрес);
	РазобратьАдресСайта(НастройкиПодключения);
	Если НастройкиПодключения.Сервер = "" Тогда
		Сообщить("Для проверки прокси - укажите начальный адрес сайта");
		Возврат;
	КонецЕсли;
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытия = Новый ДиалогВыбораФайла(Режим);
	Фильтр = НСтр("ru = 'Текст'") + "(*.txt)|*.txt";
	ДиалогОткрытия.Фильтр = Фильтр;
	ДиалогОткрытия.МножественныйВыбор = Ложь;
	ДиалогОткрытия.Заголовок = "Выберете файл для загрузки";
 
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗагрузкиПрокси", ЭтаФорма);
 
	ДиалогОткрытия.Показать(ОписаниеОповещения);
КонецПроцедуры


